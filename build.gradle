buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.liquibase:liquibase-core:5.0.0'
    }
}

plugins {
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.16.0'
    id 'org.liquibase.gradle' version '3.1.0'
    id 'java'
}

group = 'org.example'
version = '1.0.0'

repositories {
    mavenCentral()
}

ext {
    mapstructVersion = '1.6.3'
    lombokVersion = '1.18.42'
    lombokMapstructBindingVersion = '0.2.0'
    springDocsVersion = '3.0.1'
    hibernateProcessorVersion = '7.2.4.Final'
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-liquibase'

    // Springdoc OpenAPI
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocsVersion}"

    // PostgreSQL
    runtimeOnly "org.postgresql:postgresql"

    // Liquibase
    liquibaseRuntime 'org.liquibase:liquibase-core:5.0.0'
    liquibaseRuntime 'org.postgresql:postgresql'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'

    // Hibernate Processor
    compileOnly "org.hibernate.orm:hibernate-processor:${hibernateProcessorVersion}"
    annotationProcessor "org.hibernate.orm:hibernate-processor:${hibernateProcessorVersion}"

    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // MapStruct
    compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    // Encrypting
    implementation 'org.bouncycastle:bcprov-jdk18on:1.83'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

liquibase {
    activities {
        main {
            outputFile("src/main/resources/db/changelog/db.changelog-base.xml")
            url('jdbc:postgresql://localhost:5432/postgres')
            username('postgres')
            password('password')
            driver("org.postgresql.Driver")
        }
        runList("main")
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll([
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.defaultInjectionStrategy=field',
            '-Amapstruct.unmappedSourcePolicy=IGNORE',
            '-Amapstruct.unmappedTargetPolicy=IGNORE'
    ])
}

openApiGenerate {
    generatorName = 'spring'
    library = 'spring-boot'
    inputSpec = file("$rootDir/docs/openapi.yml").toURI().toString()
    outputDir = project.layout.buildDirectory
            .dir("generated-sources/openapi")
            .get().asFile.absolutePath
    apiPackage = 'org.example.bank_rest.controller.openapi'
    modelPackage = 'org.example.bank_rest.dto'
    modelNameSuffix = 'Dto'
    generateApiTests = false
    generateModelTests = false
    skipOperationExample = true
    configOptions = [
            interfaceOnly           : 'true',
            delegatePattern         : 'false',
            generateModels          : 'true',
            useSpringBoot3          : 'true',
            useTags                 : 'true',
            dateLibrary             : 'java8',
            openApiNullable         : 'false'
    ]
}
sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-sources/openapi/src/main/java"
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

tasks.test {
    useJUnitPlatform()
}